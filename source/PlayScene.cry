import Math;
import Graphics2D;
import Random;

class PlayScene : AbstractScene {
	field gameId;
	field playerId;
	field authToken;
	field lastEventId = 0;
	
	field cameraX = null;
	field cameraY = null;
	field playersById = {};
	field userIds = [];
	
	field buttons;
	field buttonKeys;
	field lastSyncTime = 0;
	field syncRequest = null;
	
	constructor(gameId, userId, authToken) : base() {
		this.gameId = gameId;
		this.playerId = userId;
		this.authToken = authToken;
		
		cols = [SCREEN_WIDTH - 200, SCREEN_WIDTH - 130, SCREEN_WIDTH - 70];
		rows = [SCREEN_HEIGHT - 200, SCREEN_HEIGHT - 130, SCREEN_HEIGHT - 70];
		
		this.buttons = {
			'up': new UIImage(cols[1], rows[0], ImageLibrary.getAtScale('arrow_up.png', .5)),
			'down': new UIImage(cols[1], rows[2], ImageLibrary.getAtScale('arrow_down.png', .5)),
			'left': new UIImage(cols[0], rows[1], ImageLibrary.getAtScale('arrow_left.png', .5)),
			'right': new UIImage(cols[2], rows[1], ImageLibrary.getAtScale('arrow_right.png', .5)),
			'wave': new UIImage(cols[1], rows[1], ImageLibrary.getAtScale('button_wave.png', .5))
		};
		this.buttonKeys = this.buttons.keys();
		
		for (button : this.buttons.values()) {
			this.addElement(button);
		}
	}
	
	function applyPollData(pollData) {
		if (pollData == null) return;
		if (pollData.length == 0) return; // sometimes PHP converts this to an empty list.
		
		this.applyAbsolutePollData(pollData);
		
		this.applyIncrementalPollData(pollData);
		
		this.lastEventId = Math.max(this.lastEventId, pollData['event_id_max']);
	}
	
	function applyIncrementalPollData(pollData) {
		minId = pollData.get('event_id_min');
		maxId = pollData.get('event_id_max');
		events = pollData.get('events');
		if (minId == null || maxId == null || events == null) return;
		
		for (eventId = minId; eventId <= maxId; ++eventId) {
			event = events['e' + eventId];
			switch (event['type']) {
				case 'MOVE':
					data = event['data'].split(':');
					userId = Core.parseInt(data[0]);
					user = this.playersById.get(userId);
					if (user != null && !user.isYou) {
						for (i = 1; i < data.length; i += 2) {
							x = Core.parseInt(data[i]);
							y = Core.parseInt(data[i + 1]);
							user.pointQueue.add([x, y]);
						}
					}
					break;
				
				case 'JOIN':
					data = event['data'].split(':');
					userId = Core.parseInt(data[0]);
					userName = data[1];
					userX = Core.parseInt(data[2]);
					userY = Core.parseInt(data[3]);
					if (this.playersById.get(userId) == null) {
						this.playersById[userId] = new Player(this, userId, userName, userX, userY);
						this.userIds = this.playersById.keys();
						this.userIds.sort();
					}
					break;
					
				default:
					throw new Core.Exception("Unknown server event type: " + event['type'] + ' --> ' + event['data']);
			}
		}
	}
	
	function applyAbsolutePollData(pollData) {
		state = pollData.get('state');
		if (state == null) return;
		userUpdatesRaw = state['user_data'].trim();
		newPlayersById = {};
		if (userUpdatesRaw.length > 0) {
			for (update : userUpdatesRaw.split('|')) {
				parts = update.split(':');
				userId = Core.parseInt(parts[0]);
				userName = parts[1];
				userX = Core.parseInt(parts[2]);
				userY = Core.parseInt(parts[3]);
				player = this.playersById.get(userId) ?? new Player(this, userId, userName, userX, userY);
				if (!player.isYou) {
					player.x = userX;
					player.y = userY;
					player.isYou = player.id == this.playerId;
				}
				newPlayersById[userId] = player;
			}
		}
		this.playersById = newPlayersById;
		this.userIds = this.playersById.keys();
		this.userIds.sort();
	}
	
	function update(input) {
		you = this.playersById[this.playerId];
		
		if (input.pressed) {
			mx = input.mouseX;
			my = input.mouseY;
			for (buttonId : this.buttonKeys) {
				button = this.buttons[buttonId];
				if (button.inBounds(mx, my)) {
					this.buttonPushed(buttonId, input.clicked);
				}
			}
		}
		pushedKeys = input.pushedKeys;
		if (pushedKeys.get('left', false)) this.buttonPushed('left', false);
		else if (pushedKeys.get('right', false)) this.buttonPushed('right', false);
		if (pushedKeys.get('up', false)) this.buttonPushed('up', false);
		else if (pushedKeys.get('down', false)) this.buttonPushed('down', false);
		
		for (userId : this.userIds) {
			player = this.playersById[userId];
			player.update();
		}
		
		if (this.lastSyncTime < Core.currentTime() - 1 && this.syncRequest == null) {
			this.syncRequest = new NetworkMessage({
				'action': 'POLL',
				'user_id': this.playerId,
				'token': this.authToken,
				'game_id': this.gameId,
				'event_id': this.lastEventId,
			}).send();
		}
		
		if (this.syncRequest != null && this.syncRequest.isDone()) {
			if (this.syncRequest.isError()) {
				// ignore?
			} else {
				syncResult = this.syncRequest.getResponse();
				switch (syncResult['err']) {
					case 'OK':
						this.applyPollData(syncResult['poll']);
						this.lastSyncTime = Core.currentTime();
						break;
					case 'GAME_RESET':
						this.switchScene(new LoginScene("The game has restarted."));
						return;
				}
			}
			this.syncRequest = null;
		}
	}
	
	function buttonPushed(id, clickedThisFrame) {
		player = this.playersById[this.playerId];
		switch (id) {
			case 'wave':
				if (clickedThisFrame) {
					this.doWave();
				}
				break;
			case 'left':
				player.dx = -10;
				break;
			case 'right':
				player.dx = 10;
				break;
			case 'up':
				player.dy = -10;
				break;
			case 'down':
				player.dy = 10;
				break;
		}
	}
	
	function doWave() {
		print("Do wave.");
	}
	
	function render(rc) {
		Draw.fill(255, 255, 255);
		player = this.playersById[this.playerId];
		targetX = player.x;
		targetY = player.y;
		if (this.cameraX == null) {
			this.cameraX = targetX;
			this.cameraY = targetY;
		} else {
			this.cameraX = (this.cameraX * 19 + targetX) / 20.0;
			this.cameraY = (this.cameraY * 19 + targetY) / 20.0;
		}
		
		cameraOffsetX = Math.floor(-this.cameraX + SCREEN_WIDTH / 2 + .5);
		cameraOffsetY = Math.floor(-this.cameraY + SCREEN_HEIGHT / 2 + .5);
		
		this.renderBackground(cameraOffsetX, cameraOffsetY);
		
		for (userId : this.userIds) {
			player = this.playersById[userId];
			player.render(cameraOffsetX, cameraOffsetY);
		}
	}
	
	field randNums = this.getRandNums();
	function getRandNums() {
		output = [];
		for (y = 0; y < 20; ++y) {
			col = [];
			for (x = 0; x < 20; ++x) {
				col.add(Random.randomInt(200, 250));
			}
			output.add(col);
		}
		return output;
	}
	
	function renderBackground(cxOffset, cyOffset) {
		colMin = Math.floor(this.cameraX - this.cameraX % 100 - SCREEN_WIDTH / 2);
		rowMin = Math.floor(this.cameraY - this.cameraY % 100 - SCREEN_HEIGHT / 2);
		colMax = colMin + SCREEN_WIDTH;
		rowMax = rowMin + SCREEN_HEIGHT;
		for (col = colMin; col <= colMax; col += 100) {
			for (row = rowMin; row <= rowMax; row += 100) {
				cn = col / 100;
				rn = row / 100;
				color = this.randNums[cn % 20][rn % 20];
				
				if (Math.abs(cn) < 15 && Math.abs(rn) < 15) {
					if (Math.abs(cn) < 10 && Math.abs(rn) < 7) {
						r = color;
						g = 255;
						b = color;
					} else {
						r = color;
						g = color;
						b = 255;
					}
				} else {
					r = color / 3;
					g = color / 4;
					b = color / 4;
				}
				
				Draw.rectangle(col + cxOffset, row + cyOffset, 100, 100, r, g, b, 255);
			}
		}
	}
}