import Game;

const FPS = 60;
const SCREEN_WIDTH = 800;
const SCREEN_HEIGHT = 600;

function main() {
	window = new GameWindow("Waving Simulator 2017", FPS, SCREEN_WIDTH, SCREEN_HEIGHT);
	activeScene = new LoadingScene();
	input = new InputState();
	rc = 0;
	
	while (true) {
		input.clicked = false;
		for (event : window.pumpEvents()) {
			switch (event.type) {
				case EventType.QUIT:
					return;
					
				case EventType.MOUSE_LEFT_DOWN:
					input.clicked = true;
					input.pressed = true;
					input.mouseX = event.x;
					input.mouseY = event.y;
					break;
					
				case EventType.MOUSE_LEFT_UP:
					input.pressed = false;
					input.mouseX = event.x;
					input.mouseY = event.y;
					break;
					
				case EventType.MOUSE_MOVE:
					input.mouseX = event.x;
					input.mouseY = event.y;
					break;
				
				case EventType.KEY_DOWN:
				case EventType.KEY_UP:
					key = null;
					switch (event.key) {
						case KeyboardKey.UP:
						case KeyboardKey.W:
							key = 'up';
							break;
						case KeyboardKey.DOWN:
						case KeyboardKey.S:
							key = 'down';
							break;
						case KeyboardKey.LEFT:
						case KeyboardKey.A:
							key = 'left';
							break;
						case KeyboardKey.RIGHT:
						case KeyboardKey.D:
							key = 'right';
							break;
					}
					if (key != null) {
						input.pushedKeys[key] = event.type == EventType.KEY_DOWN;
					}
					break;
			}
		}
		
		activeScene.doHitTesting(input);
		activeScene.update(input);
		activeScene.renderUI(rc++);
		
		if (activeScene._next != null) {
			activeScene = activeScene._next;
			activeScene._next = null;
		}
		
		window.clockTick();
	}
}
